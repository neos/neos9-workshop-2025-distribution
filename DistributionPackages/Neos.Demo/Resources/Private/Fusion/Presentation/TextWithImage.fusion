prototype(Neos.Demo:Presentation.TextWithImage) < prototype(Neos.Fusion:Component) {
    // This is used for the living styleguide (Monocle)
    // Read more about this in the README.md
    @styleguide.props {
        renderDummyImage = true
        text = '<h2>Lorem ipsum dolor sit amet,</h2>
        <p>consetetur sadipscing elitr, <strong><em>sed diam</em> nonumy eirmod</strong> tempor <code>invidunt</code> ut <em>labore et dolore</em> magna aliquyam erat, sed diam voluptua.</p>
        <p>At vero <a href="#">eos et accusam et</a> justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est Lorem ipsum dolor sit amet.</p>
        <ul><li>List item 1</li><li>List item 2</li><li>List item 3<ul><li>List item 3.1</li><li>List item 3.2</li><li>List item 3.3</li></ul></li></ul>
        <ol><li>List item 1</li><li>List item 2</li><li>List item 3<ol><li>List item 3.1</li><li>List item 3.2</li><li>List item 3.3</li></ol></li></ol>'
    }

    text = null
    renderDummyImage = false
    src = null

    alt = null
    title = null
    class = null

    hasCaption = false
    caption = null


    @if.hasText = ${this.text || (this.src || this.renderDummyImage)}

    renderer = afx`
        <div class="md:flex md:flex-wrap md:gap-4 md:flex-row">
            <Neos.Demo:Presentation.Image {...props} class="md:mb-0" imageClass="w-full max-w-none" />
            <div class="min-w-[30ch] flex-1">{props.text}</div>
        </div>
    `
}
